# vim:expandtab:shiftwidth=2:tabstop=2:

# Copyright (C) 2016 Canonical Ltd.

# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.

# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

declare_args() {
  enable_gn_build = false

  enable_tests = false

  enable_chromium_tests = false

  oxide_platform = false
}

assert(oxide_platform != false)

import("//oxide/$oxide_platform/config.gni")

assert(defined(oxide_platform_targets))

copy("copy_host_gn") {
  deps = [ "//tools/gn($host_toolchain)" ]

  source_dir = get_label_info("//tools/gn($host_toolchain)", "root_out_dir")
  sources = [ "$source_dir/gn" ]

  outputs = [ "$root_out_dir/buildtools/{{source_file_part}}" ]
}

group("host_gn") {
  deps = [
    "//tools/gn($host_toolchain)",
    ":copy_host_gn"
  ]
}

group("oxide_all") {
  # FIXME: Split test targets out of this one
  testonly = true

  deps = []

  if (enable_gn_build) {
    deps += [ ":host_gn" ]
  }

  deps += oxide_platform_targets

  if (is_linux) {
    deps += [
      "//sandbox/linux:chrome_sandbox"
    ]
  }

  if (enable_tests) {
    if (defined(oxide_platform_test_targets)) {
      deps += oxide_platform_test_targets
    }
    deps += [ "//oxide/shared:shared_unittests" ]
  }

  if (enable_chromium_tests) {
    deps += [ "//base:base_unittests" ]
  }
}
