CONFIG += testcase
QT += qml qmltest oxide-private

include($${OXIDE_QMAKE_PATH}/oxide_variables.pri)

QT.oxide.name = oxide-qt
QT.oxide.libs = $$CHROMIUM_LIB_DIR
QT.oxide.run_depends = core gui quick qml

check.commands = OXIDE_NO_SANDBOX=1
check.commands += python $${OXIDE_SRC_ROOT}/qt/tests/utils/runtests.py -s ${OXIDE_SRC_ROOT}/qt/tests/html -p 8080 --

!isEmpty(IMPORTPATH) {
    load(testcase)
    for(import, IMPORTPATH): check.commands += -import \"$$import\"
}

isEmpty(QMLSOURCEPATH):QMLSOURCEPATH = $${_PRO_FILE_PWD_}
DEFINES += QUICK_TEST_SOURCE_DIR=\"\\\"$${QMLSOURCEPATH}\\\"\"
