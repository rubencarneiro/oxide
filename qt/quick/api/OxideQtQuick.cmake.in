# vim:expandtab:shiftwidth=2:tabstop=2:

# Copyright (C) 2016 Canonical Ltd.

# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.

# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

@PACKAGE_INIT@

set(OxideQtQuick_FOUND TRUE)

set(OxideQtQuick_VERSION_MAJOR @PROJECT_VERSION_MAJOR@)
set(OxideQtQuick_VERSION_MINOR @PROJECT_VERSION_MINOR@)
set(OxideQtQuick_VERSION_PATCH @PROJECT_VERSION_PATCH@)
set(OxideQtQuick_VERSION_STRING @PROJECT_VERSION@)

set(OxideQtQuick_LIBRARIES OxideQt::Quick)
set(OxideQtQuick_INCLUDE_DIRS
    "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@"
    "@PACKAGE_CMAKE_INSTALL_INCLUDEDIR@/OxideQtQuick")

set(_FIND_PACKAGE_REQUIRED)
if(OxideQtQuick_FIND_REQUIRED)
  set(_FIND_PACKAGE_REQUIRED REQUIRED)
endif()

find_package(Qt5Core ${_FIND_PACKAGE_REQUIRED})
list(APPEND OxideQtQuick_INCLUDE_DIRS ${Qt5Core_INCLUDE_DIRS})
find_package(Qt5Qml ${_FIND_PACKAGE_REQUIRED})
list(APPEND OxideQtQuick_INCLUDE_DIRS ${Qt5Qml_INCLUDE_DIRS})
find_package(Qt5Quick ${_FIND_PACKAGE_REQUIRED})
list(APPEND OxideQtQuick_INCLUDE_DIRS ${Qt5Quick_INCLUDE_DIRS})

if(NOT TARGET OxideQt::Quick)
  add_library(OxideQt::Quick SHARED IMPORTED)
  set_target_properties(
      OxideQt::Quick PROPERTIES
      IMPORTED_LOCATION @PACKAGE_CMAKE_INSTALL_LIBDIR@/@OXIDE_QUICKLIB_FILENAME@
      INTERFACE_INCLUDE_DIRECTORIES ${OxideQtQuick_INCLUDE_DIRS}
      INTERFACE_LINK_LIBRARIES Qt5::Core Qt5::Qml Qt5::Quick)
endif()
