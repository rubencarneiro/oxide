# vim:expandtab:shiftwidth=2:tabstop=2:

# Copyright (C) 2016 Canonical Ltd.

# This library is free software; you can redistribute it and/or
# modify it under the terms of the GNU Lesser General Public
# License as published by the Free Software Foundation; either
# version 2.1 of the License, or (at your option) any later version.

# This library is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# Lesser General Public License for more details.

# You should have received a copy of the GNU Lesser General Public
# License along with this library; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA  02110-1301  USA

import("//build/toolchain/toolchain.gni")
import("//oxide/build/config/build_flags.gni")
import("//oxide/build/config/Qt5/moc.gni")
import("//oxide/build/config/test_executable.gni")
import("//oxide/build/config/version.gni")

# TODO: All includes should be a fully qualified path from the source root,
#  then we can remove this
config("core_include_dirs") {
  include_dirs = [
    "//oxide",
    "$root_gen_dir/oxide"
  ]
}

config("core_api_include_dir") {
  include_dirs = [
    "//oxide/qt/core/api/includes"
  ]
}

qtmoc("core_moc_gen") {
  if (use_qinputdevice) {
    defines = [ "USE_QINPUTDEVICE" ]
  }

  sources = [
    "api/oxideqcertificateerror.h",
    "api/oxideqdownloadrequest.h",
    "api/oxideqhttpauthenticationrequest.h",
    "api/oxideqloadevent.h",
    "api/oxideqmediacapturedevices.h",
    "api/oxideqnetworkcallbackevents_p.h",
    "api/oxideqnavigationrequest.h",
    "api/oxideqnewviewrequest.h",
    "api/oxideqpermissionrequest.h",
    "api/oxideqfindcontroller.h",
    "api/oxideqsecuritystatus.h",
    "api/oxideqsslcertificate.h",
    "api/oxideqwebcontextmenuparams.h",
    "api/oxideqwebpreferences.h",
    "browser/oxide_qt_browser_platform_integration.h",
    "browser/clipboard/oxide_qt_clipboard.h",
    "browser/contents_view_impl.h",
    "browser/input/qt_input_method_context.h",
    "browser/oxide_qt_location_provider.cc",
    "browser/oxide_qt_url_request_delegated_job.cc",
    "browser/qt_screen.h",
  ]
}

qtmoc("core_unittests_moc_gen") {
  sources = [ "browser/ssl/oxide_qt_security_status_unittest.cc" ]
}

version_header("oxide_version") {
  input_file = "common/oxide_version.h.in"
  output_file = "$target_gen_dir/common/oxide_version.h"
  version_file = "//oxide/qt/VERSION"
}

source_set("core_sources") {
  defines = [
    "OXIDE_QTCORE_IMPLEMENTATION",
    "QT_NO_SIGNALS_SLOTS_KEYWORDS"
  ]

  public_configs = [
    ":core_api_include_dir",
    ":core_include_dirs"
  ]

  deps = [
    ":core_moc_gen",
    ":oxide_version",
    "//base",
    "//components/sessions",
    "//content/public/browser",
    "//content/public/common",
    "//device/geolocation",
    "//device/vibration",
    "//device/vibration:mojo_bindings",
    "//net",
    "//oxide/build/config/Qt5:Core",
    "//oxide/build/config/Qt5:Core_Private",
    "//oxide/build/config/Qt5:Feedback",
    "//oxide/build/config/Qt5:Gui",
    "//oxide/build/config/Qt5:Gui_Private",
    "//oxide/build/config/Qt5:Network",
    "//oxide/build/config/Qt5:Positioning",
    "//oxide/shared",
    "//skia",
    "//third_party/WebKit/public:blink",
    "//ui/base",
    "//ui/base/ime:text_input_types",
    "//ui/display",
    "//ui/events",
    "//ui/events:dom_keycode_converter",
    "//ui/events:gesture_detection",
    "//ui/gfx",
    "//ui/gfx/geometry",
    "//ui/gfx/range",
    "//ui/gl",
    "//ui/touch_selection",
    "//url"
  ]

  if (use_qinputdevice) {
    deps += [ "//oxide/build/config/Qt5:SystemInfo" ]
    defines += [ "USE_QINPUTDEVICE" ]
  }

  sources = [
    "$target_gen_dir/api/moc_oxideqcertificateerror.cc",
    "$target_gen_dir/api/moc_oxideqdownloadrequest.cc",
    "$target_gen_dir/api/moc_oxideqhttpauthenticationrequest.cc",
    "$target_gen_dir/api/moc_oxideqloadevent.cc",
    "$target_gen_dir/api/moc_oxideqmediacapturedevices.cc",
    "$target_gen_dir/api/moc_oxideqnetworkcallbackevents_p.cc",
    "$target_gen_dir/api/moc_oxideqnavigationrequest.cc",
    "$target_gen_dir/api/moc_oxideqnewviewrequest.cc",
    "$target_gen_dir/api/moc_oxideqpermissionrequest.cc",
    "$target_gen_dir/api/moc_oxideqfindcontroller.cc",
    "$target_gen_dir/api/moc_oxideqsecuritystatus.cc",
    "$target_gen_dir/api/moc_oxideqsslcertificate.cc",
    "$target_gen_dir/api/moc_oxideqwebcontextmenuparams.cc",
    "$target_gen_dir/api/moc_oxideqwebpreferences.cc",
    "$target_gen_dir/browser/clipboard/moc_oxide_qt_clipboard.cc",
    "$target_gen_dir/browser/input/moc_qt_input_method_context.cc",
    "$target_gen_dir/browser/moc_oxide_qt_browser_platform_integration.cc",
    "$target_gen_dir/browser/moc_contents_view_impl.cc",
    "$target_gen_dir/browser/moc_qt_screen.cc",
    "api/oxideqcertificateerror.cc",
    "api/oxideqcertificateerror.h",
    "api/oxideqcertificateerror_p.h",
    "api/oxideqdownloadrequest.cc",
    "api/oxideqdownloadrequest.h",
    "api/oxideqglobal.cc",
    "api/oxideqglobal.h",
    "api/oxideqhttpauthenticationrequest.cc",
    "api/oxideqhttpauthenticationrequest.h",
    "api/oxideqhttpauthenticationrequest_p.h",
    "api/oxideqloadevent.cc",
    "api/oxideqloadevent.h",
    "api/oxideqmediacapturedevices.cc",
    "api/oxideqmediacapturedevices.h",
    "api/oxideqnetworkcallbackevents.cc",
    "api/oxideqnetworkcallbackevents_p.h",
    "api/oxideqnetworkcallbackevents_p_p.h",
    "api/oxideqnavigationrequest.cc",
    "api/oxideqnavigationrequest.h",
    "api/oxideqnewviewrequest.cc",
    "api/oxideqnewviewrequest.h",
    "api/oxideqnewviewrequest_p.h",
    "api/oxideqpermissionrequest.cc",
    "api/oxideqpermissionrequest.h",
    "api/oxideqpermissionrequest_p.h",
    "api/oxideqfindcontroller.cc",
    "api/oxideqfindcontroller.h",
    "api/oxideqfindcontroller_p.h",
    "api/oxideqsecuritystatus.cc",
    "api/oxideqsecuritystatus.h",
    "api/oxideqsecuritystatus_p.h",
    "api/oxideqsslcertificate.cc",
    "api/oxideqsslcertificate.h",
    "api/oxideqsslcertificate_p.h",
    "api/oxideqwebcontextmenuparams.cc",
    "api/oxideqwebcontextmenuparams.h",
    "api/oxideqwebcontextmenuparams_p.h",
    "api/oxideqwebpreferences.cc",
    "api/oxideqwebpreferences.h",
    "api/oxideqwebpreferences_p.h",
    "app/oxide_qt_allocator_hooks.h",
    "app/oxide_qt_main.cc",
    "app/oxide_qt_main.h",
    "app/oxide_qt_platform_delegate.cc",
    "app/oxide_qt_platform_delegate.h",
    "browser/chrome_controller_impl.cc",
    "browser/chrome_controller_impl.h",
    "browser/clipboard/oxide_qt_clipboard.cc",
    "browser/clipboard/oxide_qt_clipboard.h",
    "browser/contents_view_impl.cc",
    "browser/contents_view_impl.h",
    "browser/input/input_method_context_owner_client.h",
    "browser/input/qt_input_method_context.cc",
    "browser/input/qt_input_method_context.h",
    "browser/javascript_dialog_host.cc",
    "browser/javascript_dialog_host.h",
    "browser/keyboard_code_conversion.cc",
    "browser/keyboard_code_conversion.h",
    "browser/legacy_touch_editing_client_proxy.cc",
    "browser/legacy_touch_editing_client_proxy.h",
    "browser/menu_item_builder.cc",
    "browser/menu_item_builder.h",
    "browser/navigation_history_impl.cc",
    "browser/navigation_history_impl.h",
    "browser/oxide_qt_browser_platform_integration.cc",
    "browser/oxide_qt_browser_platform_integration.h",
    "browser/oxide_qt_browser_startup.cc",
    "browser/oxide_qt_browser_startup.h",
    "browser/oxide_qt_browser_thread_q_event_dispatcher.cc",
    "browser/oxide_qt_browser_thread_q_event_dispatcher.h",
    "browser/oxide_qt_dpi_utils.cc",
    "browser/oxide_qt_dpi_utils.h",
    "browser/oxide_qt_drag_source.cc",
    "browser/oxide_qt_drag_source.h",
    "browser/oxide_qt_drag_utils.cc",
    "browser/oxide_qt_drag_utils.h",
    "browser/oxide_qt_event_utils.cc",
    "browser/oxide_qt_event_utils.h",
    "browser/oxide_qt_file_picker.cc",
    "browser/oxide_qt_file_picker.h",
    "browser/oxide_qt_find_controller.cc",
    "browser/oxide_qt_find_controller.h",
    "browser/oxide_qt_location_provider.cc",
    "browser/oxide_qt_location_provider.h",
    "browser/oxide_qt_message_pump.cc",
    "browser/oxide_qt_message_pump.h",
    "browser/oxide_qt_motion_event_factory.cc",
    "browser/oxide_qt_motion_event_factory.h",
    "browser/oxide_qt_screen_utils.cc",
    "browser/oxide_qt_screen_utils.h",
    "browser/oxide_qt_script_message.cc",
    "browser/oxide_qt_script_message.h",
    "browser/oxide_qt_script_message_handler.cc",
    "browser/oxide_qt_script_message_handler.h",
    "browser/oxide_qt_script_message_request.cc",
    "browser/oxide_qt_script_message_request.h",
    "browser/oxide_qt_skutils.cc",
    "browser/oxide_qt_skutils.h",
    "browser/oxide_qt_type_conversions.h",
    "browser/oxide_qt_url_request_delegated_job.cc",
    "browser/oxide_qt_url_request_delegated_job.h",
    "browser/oxide_qt_user_script.cc",
    "browser/oxide_qt_user_script.h",
    "browser/oxide_qt_variant_value_converter.cc",
    "browser/oxide_qt_variant_value_converter.h",
    "browser/oxide_qt_vibration_manager.cc",
    "browser/oxide_qt_vibration_manager.h",
    "browser/oxide_qt_web_context.cc",
    "browser/oxide_qt_web_context.h",
    "browser/oxide_qt_web_frame.cc",
    "browser/oxide_qt_web_frame.h",
    "browser/oxide_qt_web_view.cc",
    "browser/oxide_qt_web_view.h",
    "browser/qt_screen.cc",
    "browser/qt_screen.h",
    "browser/ssl/oxide_qt_security_status.cc",
    "browser/ssl/oxide_qt_security_status.h",
    "browser/touch_handle_drawable_host.cc",
    "browser/touch_handle_drawable_host.h",
    "browser/web_contents_id_tracker.cc",
    "browser/web_contents_id_tracker.h",
    "browser/web_context_menu_host.cc",
    "browser/web_context_menu_host.h",
    "browser/web_popup_menu_host.cc",
    "browser/web_popup_menu_host.h",
    "browser/web_preferences.cc",
    "browser/web_preferences.h",
    "common/oxide_qt_export.h",
    "glue/chrome_controller.cc",
    "glue/chrome_controller.h",
    "glue/chrome_controller_client.h",
    "glue/contents_view.h",
    "glue/contents_view_client.h",
    "glue/javascript_dialog.h",
    "glue/javascript_dialog_client.h",
    "glue/javascript_dialog_type.h",
    "glue/legacy_touch_editing_client.h",
    "glue/legacy_touch_editing_controller.cc",
    "glue/legacy_touch_editing_controller.h",
    "glue/macros.h",
    "glue/menu_item.cc",
    "glue/menu_item.h",
    "glue/navigation_history.cc",
    "glue/navigation_history.h",
    "glue/navigation_history_client.h",
    "glue/oxide_qt_file_picker_proxy.h",
    "glue/oxide_qt_file_picker_proxy_client.h",
    "glue/oxide_qt_init.cc",
    "glue/oxide_qt_init.h",
    "glue/oxide_qt_proxy_base.cc",
    "glue/oxide_qt_proxy_base.h",
    "glue/oxide_qt_script_message_handler_proxy.cc",
    "glue/oxide_qt_script_message_handler_proxy.h",
    "glue/oxide_qt_script_message_handler_proxy_client.h",
    "glue/oxide_qt_script_message_proxy.h",
    "glue/oxide_qt_script_message_request_proxy.cc",
    "glue/oxide_qt_script_message_request_proxy.h",
    "glue/oxide_qt_script_message_request_proxy_client.h",
    "glue/oxide_qt_touch_handle_drawable_proxy.h",
    "glue/oxide_qt_user_script_proxy.cc",
    "glue/oxide_qt_user_script_proxy.h",
    "glue/oxide_qt_user_script_proxy_client.h",
    "glue/oxide_qt_web_context_proxy.cc",
    "glue/oxide_qt_web_context_proxy.h",
    "glue/oxide_qt_web_context_proxy_client.h",
    "glue/oxide_qt_web_frame_proxy.h",
    "glue/oxide_qt_web_frame_proxy_client.h",
    "glue/oxide_qt_web_view_proxy.cc",
    "glue/oxide_qt_web_view_proxy.h",
    "glue/oxide_qt_web_view_proxy_client.h",
    "glue/screen_utils.cc",
    "glue/screen_utils.h",
    "glue/web_context_menu.h",
    "glue/web_context_menu_actions.h",
    "glue/web_context_menu_client.h",
    "glue/web_context_menu_params.h",
    "glue/web_context_menu_sections.h",
    "glue/web_popup_menu.h",
    "glue/web_popup_menu_client.h",
    "gpu/oxide_qt_gl_context_dependent.cc",
    "gpu/oxide_qt_gl_context_dependent.h"
  ]
}

shared_library("core") {
  output_name = "$oxide_lib_name"
  output_extension = "$oxide_lib_extension"

  public_deps = [
    ":core_sources"
  ]

  if (is_linux) {
    ldflags = [
      # Needed to pick up libffmpeg.so when installed
      "-Wl,-rpath=\$ORIGIN/$oxide_libexec_subdir",
      "-Wl,--disable-new-dtags"
    ]
  }

  sources = [
    "dummy.cc"
  ]

  if (is_component_build && is_linux) {
    ldflags += [
      "-Wl,-rpath=\$ORIGIN/$oxide_libexec_subdir/$shlib_subdir"
    ]
  }
}

component("core_component") {
  output_name = "oxide_qt"

  public_deps = [
    ":core_sources"
  ]

  sources = [
    "dummy.cc"
  ]
}

test_executable("core_unittests") {
  output_name = "oxide_qt_unittests"

  defines = [
    "QT_NO_SIGNALS_SLOTS_KEYWORDS",
  ]

  deps = [
    ":core_component",
    ":core_unittests_moc_gen",
    "//base",
    "//base/test:test_support",
    "//content/public/browser",
    "//content/public/common",
    "//content/test:test_support",
    "//mojo/edk/system",
    "//net",
    "//testing/gtest",
    "//oxide/build/config/Qt5:Core",
    "//oxide/build/config/Qt5:Gui",
    "//oxide/shared",
    "//oxide/shared:shared_testutils"
  ]

  sources = [
    "browser/ssl/oxide_qt_security_status_unittest.cc",
    "test/run_all_unittests.cc",
  ]
}

test_executable("core_screen_unittests") {
  output_name = "oxide_qt_screen_unittests"

  defines = [
    "QT_NO_SIGNALS_SLOTS_KEYWORDS",
  ]

  deps = [
    ":core_component",
    "//oxide/build/config/Qt5:Core",
    "//oxide/build/config/Qt5:Gui",
    "//oxide/shared",
    "//oxide/shared:shared_testutils",
    "//testing/gmock",
    "//testing/gtest",
    "//ui/display",
  ]

  if (use_qinputdevice) {
    deps += [ "//oxide/build/config/Qt5:SystemInfo" ]
    defines += [ "USE_QINPUTDEVICE" ]
  }

  sources = [
    "browser/qt_screen_unittest.cc",
    "test/run_all_unittests.cc",
  ]
}
