CONFIG += testcase
QT += qml qmltest oxide

include($${OXIDE_QMAKE_PATH}/oxide_variables.pri)

_DESTDIR = $${OXIDE_BUILD_OUT}/test

QT.oxide.name = OxideQtCore
QT.oxide.libs = $$CHROMIUM_OUT_LIB_DIR

isEmpty(TESTDATAPATH):TESTDATAPATH = $${_PRO_FILE_PWD_}

check.commands = OXIDE_NO_SANDBOX=1
check.commands += OXIDE_RESOURCE_PATH=$$CHROMIUM_OUT_PLAT_DIR
check.commands += python $${OXIDE_SRC_ROOT}/qt/tests/utils/runtests.py -s $${TESTDATAPATH} -p 8080
oxideqmltestcase-tmp-profile:check.commands += -t
check.commands += --
load(testcase)

check.commands = cd $$_DESTDIR && $$check.commands

DESTDIR = $$_DESTDIR

!isEmpty(IMPORTPATH) {
    for(import, IMPORTPATH): check.commands += -import \"$$import\"
}

DEFINES += QUICK_TEST_SOURCE_DIR=\"\\\"$${TESTDATAPATH}\\\"\"
