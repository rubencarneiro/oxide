TEMPLATE = aux

include($${OXIDE_QMAKE_PATH}/oxide_variables.pri)

GYP_MAKEFILE = Makefile.oxide
GYP_MAKE_INVOKE = CFLAGS= CXXFLAGS= LDFLAGS= CPPFLAGS= make -C $$OXIDE_SRC_ROOT -f $$GYP_MAKEFILE $$TARGET

isEmpty(QMAKE_EXTENSION_SHLIB):QMAKE_EXTENSION_SHLIB = so

gyp_generate.target = $${OXIDE_SRC_ROOT}/$${GYP_MAKEFILE}
gyp_generate.commands = cd $${OXIDE_SRC_ROOT}; ./build/gyp_oxide -I$${OXIDE_SRC_ROOT}/qt/qt.gypi -Doxide_qt_libversion=$$OXIDE_QT_LIBVERSION
QMAKE_EXTRA_TARGETS += gyp_generate

gyp_make.target = gyp_make
gyp_make.depends = gyp_generate
equals(OXIDE_DEBUG, "1") {
    gyp_make.commands = $$GYP_MAKE_INVOKE V=1
} else {
    gyp_make.commands = $$GYP_MAKE_INVOKE BUILDTYPE=Release
}
QMAKE_EXTRA_TARGETS += gyp_make
PRE_TARGETDEPS += gyp_make

equals(GYP_TYPE, lib) {
    LIB0 = $${QMAKE_PREFIX_SHLIB}$${TARGET}.$${QMAKE_EXTENSION_SHLIB}
    LIB1 = $${LIB0}.$${GYP_LIBVERSION}
    add_symlink.target = add_symlink
    add_symlink.commands = cd $${CHROMIUM_OUT_LIB_DIR}; ln -f -s $$LIB1 $$LIB0
    QMAKE_EXTRA_TARGETS += add_symlink
    POST_TARGETDEPS += add_symlink

    install_lib.path = $$LIBDIR
    install_lib.files = $${CHROMIUM_OUT_LIB_DIR}/$${LIB0} $${CHROMIUM_OUT_LIB_DIR}/$${LIB1}
    install_lib.CONFIG = no_check_exist
    INSTALLS += install_lib
} else:equals(GYP_TYPE, libexec) {
    install_bin.path = $$LIBEXECDIR
    isEmpty(TARGET.bin_name) {
        install_bin.files = $${CHROMIUM_OUT_PLAT_DIR}/$${TARGET}
    } else {
        install_bin.files = $${CHROMIUM_OUT_PLAT_DIR}/$${TARGET.bin_name}
        install_bin.extra = cp $${CHROMIUM_OUT_PLAT_DIR}/$${TARGET} $${CHROMIUM_OUT_PLAT_DIR}/$${TARGET.bin_name}
    }
    install_bin.CONFIG = no_check_exist
    INSTALLS += install_bin

    !isEmpty(TARGET.post_install) {
        install_bin_postinst.path = $$LIBEXECDIR
        isEmpty(TARGET.bin_name):install_bin_postinst.commands = $$TARGET.post_install $${LIBEXECDIR}/$${TARGET}
        else:install_bin_postinst.commands = $$TARGET.post_install $${LIBEXECDIR}/$${TARGET.bin_name}
        INSTALLS += install_bin_postinst
    }
}
